apply plugin: 'java'
apply plugin: 'eclipse'

apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.kevinstl"
archivesBaseName = "CoinbaseJava"

sourceCompatibility = 1.6
version = '0.1.0'
jar {
    manifest {
        attributes 'Implementation-Title': 'CoinbaseJava', 'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

repositories {
    mavenCentral()
}

sourceSets {

		main {
			java {
				srcDir 'src/main/java'
				srcDir 'src/main/resources'
			}
		}

    test {
        java {
            srcDir 'src/test/unit'
            srcDir 'src/test/cucumber'
            srcDir 'src/test/integration'
            srcDir 'src/test/resources'
        }       
    }
}

dependencies {
    testCompile  'junit:junit:4.+'
    testCompile  'org.mockito:mockito-all:1.9.0'
    testCompile  'org.easymock:easymock:3.1'
    testCompile  'org.powermock:powermock-mockito-release-full:1.4.12'
    testCompile  'org.powermock:powermock-easymock-release-full:1.4.12'
    
    testCompile  'org.seleniumhq.selenium:selenium-java:2.25.0'
    
    testCompile  'info.cukes:cucumber-java:1.1.2'
    testCompile  'info.cukes:cucumber-core:1.1.2'
    testCompile  'info.cukes:cucumber-junit:1.1.2'
    testCompile  'info.cukes:cucumber-spring:1.1.2'
    testCompile  'info.cukes:gherkin:2.11.2'
    
    testCompile  'org.springframework:spring-test:3.2.1.RELEASE'
    
    testCompile  'org.hamcrest:hamcrest-all:1.3'
    
    
    compile 'commons-collections:commons-collections:3.2'
    compile 'commons-codec:commons-codec:1.7'
    compile 'commons-lang:commons-lang:2.6'
    compile 'org.apache.httpcomponents:httpclient:4.2.5'
    
    
    compile 'org.springframework:spring-core:3.2.1.RELEASE'
    compile 'org.springframework:spring-context:3.2.1.RELEASE'
    
    
    compile 'com.google.code.gson:gson:2.2.4'
            
    
    compile fileTree(dir: 'lib', includes: ['*.jar'])
	  compile fileTree(dir: '../repos', includes: ['*.jar'])
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'CoinbaseJava'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Coinbase API Java Client Library.'
        url 'https://github.com/kevinstl/CoinbaseJava'

        scm {
          connection 'https://github.com/kevinstl/CoinbaseJava.git'
          developerConnection 'https://github.com/kevinstl/CoinbaseJava.git'
          url 'https://github.com/kevinstl/CoinbaseJava'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'kevinstl'
            name 'Kevin Wilde'
            email 'kjwilde@hotmail.com'
          }
        }
      }
    }
  }
}


